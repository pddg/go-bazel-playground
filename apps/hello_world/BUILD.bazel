load("@rules_go//go:def.bzl", "go_binary", "go_cross_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

go_binary(
    name = "hello_world",
    embed = [":hello_world_lib"],
    visibility = ["//visibility:public"],
)

[
    go_cross_binary(
        name = "hello_world_" + os + "_" + arch,
        platform = "@rules_go//go/toolchain:" + os + "_" + arch,
        target = ":hello_world",
    )
    for os in [
        "linux",
        "windows",
        "darwin",
    ]
    for arch in [
        "amd64",
        "arm64",
    ]
]

go_library(
    name = "hello_world_lib",
    srcs = [
        "main.go",
        "os_darwin.go",
        "os_linux.go",
        "os_windows.go",
    ],
    importpath = "github.com/pddg/go-bazel-playground/apps/hello_world",
    visibility = ["//visibility:private"],
    deps = [
        "//internal/reverse",
        "@com_github_google_uuid//:uuid",
    ] + select({
        "@rules_go//go/platform:windows": [
            "//internal/windows",
        ],
        "//conditions:default": [],
    }),
)

pkg_tar(
    name = "pkg",
    srcs = [":hello_world_linux_amd64"],
)

oci_image(
    name = "image_amd64",
    base = "@distroless_static_debian12_linux_amd64",
    entrypoint = ["/hello_world_linux_amd64"],
    tars = [":pkg"],
)

oci_load(
    name = "image_amd64_load",
    image = ":image_amd64",
    repo_tags = ["hello_world:latest"],
)
