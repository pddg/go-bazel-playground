load("@rules_oci//oci:defs.bzl", "oci_image_index", "oci_load", "oci_image", "oci_push")
load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("//build_tools/transitions:multi_arch.bzl", "multi_arch")

PACKAGES = [
    "@bookworm//cowsay",
    "@bookworm//fortunes",
]

expand_template(
    name = "image_annotations",
    out = "_stamped.annotations.txt",
    template = [
        "org.opencontainers.image.source=https://github.com/pddg/go-bazel-playground",
        "org.opencontainers.image.version=nightly",
        "org.opencontainers.image.revision=devel",
        "org.opencontainers.image.created=1970-01-01T00:00:00Z",
    ],
    stamp_substitutions = {
        "devel": "{{GIT_SHA}}",
        "nightly": "{{VERSION}}",
        "1970-01-01T00:00:00Z": "{{BUILD_TIMESTAMP_ISO8601}}",
    },
)

oci_image(
    name = "image",
    annotations = ":image_annotations",
    base = "@distroless_static_debian12",
    entrypoint = ["/usr/games/cowsay"],
    tars = select({
        "@platforms//cpu:x86_64": [
            pkg + "/amd64"
            for pkg in PACKAGES
        ],
        "@platforms//cpu:arm64": [
            pkg + "/arm64"
            for pkg in PACKAGES
        ],
    }),
)

ARCHS = [
    "amd64",
    "arm64",
]

[
    multi_arch(
        name = "image_" + arch,
        platforms = [
            "@rules_go//go/toolchain:linux_" + arch,
        ],
        target = ":image",
    )
    for arch in ARCHS
]

oci_image_index(
    name = "image_index",
    images = [":image_" + arch for arch in ARCHS],
)

REPOSITORY = "ghcr.io/pddg/go-bazel-playground-fortune-cowsay"

oci_load(
    name = "image_load",
    image = select({
        "@platforms//cpu:x86_64": ":image_amd64",
        "@platforms//cpu:arm64": ":image_arm64",
    }),
    repo_tags = [REPOSITORY + ":latest"],
)

expand_template(
    name = "image_tags",
    out = "_stamped.tags.txt",
    template = ["latest"],
    stamp_substitutions = {
        "latest": "{{VERSION}}",
    },
)

oci_push(
    name = "image_push",
    image = ":image_index",
    remote_tags = ":image_tags",
    repository = REPOSITORY,
)
